plugins {
    id 'org.asciidoctor.jvm.revealjs' version '3.0.0-alpha.1'
}

repositories {
    jcenter()
    maven { url 'http://rubygems-proxy.torquebox.org/releases' }
    mavenCentral()
}

configurations {
    asciidoctorExt
}

dependencies {
    asciidoctorExt group: 'com.puravida.asciidoctor', name: 'asciidoctor-barcode', version:'2.3.0'
    asciidoctorExt group: 'net.sourceforge.plantuml', name: 'plantuml', version: '1.2019.5'
    asciidoctorExt group: 'org.scilab.forge', name: 'jlatexmath', version: '1.0.7'
}

ext{
    documentVersion = '0.0.1'
}

asciidoctorj {

    version '2.0.0'

    modules{
        diagram{
            version '1.5.16'
        }
    }

    options doctype: 'book', ruby:'erubis'

    attributes 'source-highlighter': 'coderay',
            toc: '',
            idprefix: '',
            idseparator: '-',
            'icons': 'font',
            'setanchors': '',
            'listing-caption':'',
            version: documentVersion,
            revnumber: documentVersion

    docExtensions {
        block_macro (name: 'tweet') { parent, target, attributes ->
            String content = """<div class="tweet" data-src="https://twitter.com/${target}/status/${attributes.get('1')}"></div>"""
            config.remove 'content_model'
            createBlock(parent, "pass", [content], [:], config)
        }

        block(name: 'spreadsheet', contexts: [':listing']) { parent, reader, attributes ->
            def content =['<div class="spreadsheet" data-delimiter="|">']
            content.addAll reader.readLines()
            content.add '</div>'
            createBlock(parent, 'pass', content, attributes, [:])
        }

        postprocessor{
            document, output ->
                output = output.replace("<body>","""
                <script src="reveal.js/plugin/rajgoel/spreadsheet/ruleJS.all.full.min.js"></script>
                <link rel="stylesheet" href="reveal.js/plugin/rajgoel/spreadsheet/spreadsheet.css">
                <body>
                """)
                output
        }
        
    }
    
}


revealjsPlugins{
    github 'rajgoel', {
        organisation = 'rajgoel'
        repository = 'reveal.js-plugins'
        branch = 'master'
    }
    github 'denehyg', {
        organisation = 'denehyg'
        repository = 'reveal.js-menu'
        branch = 'master'
    }
}

asciidoctorRevealJs{

    //baseDirFollowsSourceDir()
    logDocuments true
    configurations 'asciidoctorExt'
    sources {
        include '*.adoc'
    }

    asciidoctorj {
        attributes 'sourceDir': 'src/docs/asciidoc',
                'imagesDir': 'images',
                'icons': 'font',
                'iconfont-name': 'fontawesome-5.8.0'
    }

    revealjsOptions {
        controls = true
        slideNumber = true
        progressBar = true
        pushToHistory = true
        overviewMode = true
        touchMode = true
        backgroundTransition = 'convex' //none , fade, slide, convex, concave, zoom
        theme = 'sky' //'black', 'beige' , 'league', 'night', 'serif', 'simple', 'sky', 'solarized'
    }

    plugins 'rajgoel/chart/Chart.min.js',
            'rajgoel/chart/csv2chart.js',
            'rajgoel/chalkboard/chalkboard.js',
            'rajgoel/embed-tweet/embed-tweet.js',
            'rajgoel/spreadsheet/spreadsheet.js',
            'denehyg/menu.js'

    pluginConfigurationFile 'src/docs/asciidoc/plugin-configuration.js'

}
build.dependsOn asciidoctorRevealJs