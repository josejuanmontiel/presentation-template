buildscript {
    dependencies {
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0'
        classpath 'commons-httpclient:commons-httpclient:3.1'

        classpath 'org.asciidoctor:asciidoctorj-diagram:1.5.8'
        classpath group: 'net.sourceforge.plantuml', name: 'plantuml', version: '1.2018.6'
        classpath group: 'org.scilab.forge', name: 'jlatexmath', version: '1.0.6'
    }
}

plugins {
  id 'org.asciidoctor.convert' version '1.5.3'
  id 'com.github.jruby-gradle.base' version '1.4.0'
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'

version = '1.0.0-SNAPSHOT'

ext {
    revealjsVersion = '3.3.0'
    asciidoctorBackendVersion = '1.1.3'
    downloadDir = new File(buildDir,'download')
    templateDir = new File(downloadDir,'templates')
    revealjsDir   = new File(downloadDir,'reveal.js')
}

jruby {
    defaultRepositories false
}

repositories {
    jcenter()
    maven { url 'http://rubygems-proxy.torquebox.org/releases' }
}

dependencies {
    gems 'rubygems:slim:3.0.8'
    gems 'rubygems:thread_safe:0.3.5'
    compile 'org.asciidoctor:asciidoctorj:1.5.6'
    asciidoctor 'org.asciidoctor:asciidoctorj-diagram:1.5.8'
}

task download {
    doLast {
        mkdir downloadDir
        vfs {
            cp "zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/v${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
            templateDir, recursive:true, overwrite:true
            cp "zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
            revealjsDir, recursive:true, overwrite:true
        }
    }
}

download {
    description "Download extra revealjs resources"
    outputs.dir templateDir
    outputs.dir revealjsDir
}

asciidoctorj {
  version = '1.5.6'
}

asciidoctor {
    dependsOn jrubyPrepare, download
    sources {
        include 'index.adoc'
    }
    resources {
        from (sourceDir) {
            include 'images/**'
        }
    }
    resources {
        from ( new File(downloadDir,"reveal.js/reveal.js-${revealjsVersion}") ){
            include "**"
        }
        into 'reveal.js'
    }
    backends 'revealjs'
    requires 'asciidoctor-diagram'
    requires 'tilt'
    attributes \
        'build-gradle': file('build.gradle'),
        'sourcedir': project.sourceSets.main.java.srcDirs[0],
        'endpoint-url': 'http://example.org',
        'source-highlighter': 'coderay',
        'imagesdir': './images',
        'toc': 'left',
        'icons': 'font',
        'setanchors': '',
        'idprefix': 'slide-',
        'idseparator': '-',
        'docinfo1': '',
        'revealjs_theme': 'black',
        'revealjs_transition': 'linear',
        'revealjs_history': 'true',
        'revealjs_slideNumber': 'true'
    options template_dirs: [ new File(new File(templateDir,'asciidoctor-reveal.js-1.1.3'),'templates').absolutePath]
}
asciidoctor.dependsOn download
