buildscript {
    dependencies {
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0'
        classpath 'commons-httpclient:commons-httpclient:3.1'
        classpath group: 'net.sourceforge.plantuml', name: 'plantuml', version: '1.2018.6'
        classpath group: 'org.scilab.forge', name: 'jlatexmath', version: '1.0.6'
    }
}

plugins {
  id 'org.asciidoctor.convert' version '1.5.3'
  id 'com.github.jruby-gradle.base' version '1.4.0'
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'

import org.asciidoctor.gradle.AsciidoctorTask

version = '1.0.0-SNAPSHOT'

ext {
    revealjsVersion = '3.7.0'
    asciidoctorBackendVersion = '1.1.3'
    downloadDir = new File(buildDir,'download')
    templateDir = new File(downloadDir,'templates')
    revealjsDir   = new File(downloadDir,'reveal.js')
    sourceAdoc = project.hasProperty("ppt") ? ppt : "index.adoc"
}

jruby {
    defaultRepositories false
}

asciidoctorj {
  version = '1.6.0-alpha.7'
}

repositories {
    jcenter()
    maven { url 'http://rubygems-proxy.torquebox.org/releases' }
    maven { url  "https://dl.bintray.com/puravida-software/repo" }
}

dependencies {
    gems 'rubygems:slim:3.0.8'
    gems 'rubygems:thread_safe:0.3.5'
    asciidoctor 'com.puravida.asciidoctor:asciidoctor-barcode:1.8.1-dev.22+bdd788f'
    asciidoctor 'org.asciidoctor:asciidoctorj-diagram:1.5.9'
}

task download {
    doLast {
        mkdir downloadDir
        vfs {
            cp "zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/v${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
            templateDir, recursive:true, overwrite:true
            cp "zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
            revealjsDir, recursive:true, overwrite:true
        }
    }
}

download {
    description "Download extra revealjs resources"
    outputs.dir templateDir
    outputs.dir revealjsDir
}

file('src/docs/asciidoc').eachFileMatch(~/.*.adoc/) { f ->  
    task "asciidoctor_$f.name"(type:AsciidoctorTask) {
        dependsOn jrubyPrepare, download
        sources {
            include f.name
        }
        resources {
            from (sourceDir) {
                include 'images/**'
            }
        }
        resources {
            from ( new File(downloadDir,"reveal.js/reveal.js-${revealjsVersion}") ){
                include "**"
            }
            into 'reveal.js'
        }
        backends 'revealjs'
        requires 'asciidoctor-diagram'
        requires 'tilt'
        attributes \
            'sourcedir': project.sourceSets.main.java.srcDirs[0],
            'source-highlighter': 'coderay',
            'imagesdir': './images',
            'toc': 'left',
            'icons': 'font',
            'setanchors': '',
            'idprefix': 'slide-',
            'idseparator': '-',
            'docinfo1': ''
        options template_dirs: [ new File(new File(templateDir,'asciidoctor-reveal.js-1.1.3'),'templates').absolutePath]
    }
    tasks["asciidoctor_$f.name"].dependsOn download
    build.dependsOn tasks["asciidoctor_$f.name"]
}

