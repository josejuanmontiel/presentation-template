plugins {
    id 'org.asciidoctor.jvm.revealjs' version '2.0.0'
}

import org.asciidoctor.gradle.jvm.slides.AsciidoctorJRevealJSTask

repositories {
    jcenter()
    maven { url 'http://rubygems-proxy.torquebox.org/releases' }
    maven { url  "https://dl.bintray.com/puravida-software/repo" }
}

configurations {
    asciidoctorExt
}

dependencies {
    asciidoctorExt 'com.puravida.asciidoctor:asciidoctor-barcode:2.0.0'
    asciidoctorExt 'net.sourceforge.plantuml:plantuml:1.2019.4'
    asciidoctorExt 'org.scilab.forge:jlatexmath:1.0.7'
}

asciidoctorj {
    diagramVersion '1.5.16'
}

revealjs {
    //version = '2.0.0'
    templateGitHub {
        organisation = 'hakimel'
        repository = 'reveal.js'
        tag = '3.8.0'
    }
}

revealjsPlugins{
    github 'rajgoel', {
        organisation = 'rajgoel'
        repository = 'reveal.js-plugins'
        branch = 'master'
    }
}


file('src/docs/asciidoc').eachFileMatch(~/.*.adoc/) { f ->

    String name = f.name.split('\\.').dropRight(1).join('_')

    task "${name}"(type:AsciidoctorJRevealJSTask) {
        group 'slides'

        dependsOn tasks['asciidoctorGemsPrepare']

        configurations 'asciidoctorExt'

        outputDir = { "${project.buildDir}/docs/slides" }
        sourceDir 'src/docs/asciidoc'
        sources {
            include '*.adoc'
        }

        attributes 'sourceDir': 'src/docs/asciidoc',
                'groovyDir': 'src/docs/asciidoc',
                'imagesDir': 'images',
                'source-highlighter': 'coderay',
                'toc': 'left',
                'icons': 'font',
                'setanchors': '',
                'idprefix': 'slide-',
                'idseparator': '-',
                'docinfo1': ''

        revealjsOptions {
            controls = true
            slideNumber = true
            progressBar = true
            pushToHistory = true
            overviewMode = true
            touchMode = true
            backgroundTransition = 'convex' //none , fade, slide, convex, concave, zoom

            parallaxBackgroundImage = 'src/docs/asciidoc/images/parallax.jpg'
            parallaxBackgroundSize = '2100px 900px'

            theme = 'sky' //'black', 'beige' , 'league', 'night', 'serif', 'simple', 'sky', 'solarized'
        }

        toggleBuiltinPlugin 'pdf', true
        toggleBuiltinPlugin 'notes', true

        resources {
            from('src/docs/resources') {
                include '**'
            }
            into '.'
        }
    }

    task "hack_${name}"() {
        group 'slides'
        dependsOn tasks["$name"]
        doLast{
            File html = file( "$buildDir.absolutePath/docs/slides/${name}.html")
            File extra = file("$buildDir.absolutePath/docs/slides/reveal.js/revealjs-plugins.js")
            html.text = html.text.replace('dependencies: [', 'dependencies: ['+extra.text)
        }
    }
    build.dependsOn tasks["hack_${name}"]
}
